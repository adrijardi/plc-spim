// CUP specification for a simple expression evaluator (no actions)
import java_cup.runtime.*;
import java.io.IOException;
import java.util.LinkedList;

/* Preliminaries to set up and use the scanner.  */
parser code{:
	private Lexer lex;
	private Syntactic sin;
	private boolean error;
	private java.util.LinkedList<AnalyzerError> errorsList;
	private java.util.LinkedList<AnalyzerError> warningList;
	private NodeAnalyzer main;
	private StateAnalyzer state;
	//TODO quitar luego
	private int auxVar = 0;
	int auxCountParamDec = 0;
	private Generator generator;

	public static void main(String args[]) throws Exception{
		System.out.println("Con argumentos " + args[0]);
		Lexer lex = new Lexer(new java.io.FileReader(args[0]));
		FunTable.getInstance();
		Syntactic sin = new Syntactic(lex);
		sin.initializeParser(lex, args[0]);
		sin.runParser();
	}
	
	private void initializeParser(Lexer lexer, String filename) throws IOException{
		generator = new Generator(filename);
		error = false;
		lex = lexer;
		errorsList = new java.util.LinkedList<AnalyzerError>();
		warningList = new java.util.LinkedList<AnalyzerError>();
		lookahead = new Symbol[]{new Symbol(sym.SEMICOLON), new Symbol(sym.RBRACE)};
		state = new StateAnalyzer();
	}
	
	public StateAnalyzer getState() {
		return state;
	}
	
	public int getLine(){
		return lex.getLine();
	}
	
	private void runParser(){
		
		try {
			parse();
		} catch (Exception e) {
			// TODO Auto-generated catch block
			e.printStackTrace();
		}
		
		checkVariables();
		
		showWarnings();
		
		if(isError()){
			showErrors();
		}else{
			generate();
			VarTable.getInstance().printTable();
			FunTable.getInstance().printTable();
		}
	}
	
	private void showErrors(){
		System.out.println("No se generara codigo, se han encontrado "+errorsList.size()+" errores");
		for (AnalyzerError ae : errorsList) {
			System.out.println(ae);
		}
	}
	
	private void showWarnings(){
		System.out.println("Se han encontrado "+warningList.size()+" Warnings");
		for (AnalyzerError ae : warningList) {
			System.out.println(ae.toWarningString());
		}
	}
	
	private void generate(){
		generator.setMain(main);
		generator.generate();
	}
	
	private boolean isError() {
		return this.error;
	}
	
	public void addError(int line, String msg){
		System.out.println("!!!!! ERROR !!!!");
		if(this.error == false)
			this.error = true;
		this.errorsList.add(new AnalyzerError(line,msg));
	}
	
	public void addError(String msg, int lineMod){
		addError(getLine()+ lineMod, msg);
	}
	
	public void addError(String msg){
		addError(getLine(), msg);
	}
	
	public void addWarning(int line, String msg){		
		this.warningList.add(new AnalyzerError(line,msg));
	}
	
	public void addWarning(String msg, int lineMod){
		addWarning(getLine()+lineMod, msg);
	}
	
	public void addWarning(String msg){
		addWarning(getLine(), msg);
	}
	
	public void setMain(NodeAnalyzer main) {
		this.main = main;
	}
	
	public void checkVariables(){
		main.checkVariables(this);
	}
	
	//TODO quitar luego
	public int getAuxVar(){
		return auxVar++;
	}
	
:};

/* Terminals (tokens returned by the scanner). */
terminal	PRINTF;
terminal	GETS;
terminal	VOID;
terminal	IF;
terminal	ELSE;
terminal	WHILE;
terminal	RETURN;
terminal 	NOTEQ;
terminal	NOT;
terminal	LTEQ;
terminal	LT;
terminal	GTEQ;
terminal	GT;
terminal	EQEQ;
terminal	EQ;
terminal	ANDAND;
terminal	OROR;
terminal	MULTEQ;
terminal	MULT;
terminal	DIVEQ;
terminal	DIV;
terminal	PLUSPLUS;
terminal	PLUSEQ;
terminal	PLUS;
terminal	MINUSMINUS;
terminal	MINUSEQ;
terminal	MINUS;
terminal	LPAREN;
terminal	RPAREN;
terminal	LBRACE;
terminal	RBRACE;
terminal	LBRACK;
terminal	RBRACK;
terminal	SEMICOLON;
terminal	COMMA;
terminal	ID; 
terminal Integer    INTEGER;
terminal Integer	INT;
terminal Float		NUMBERF;
terminal Float		FLOAT;
terminal Character	CHARACTER;
terminal Character	CHAR;
terminal String		STRING;

/* Non terminals */
non terminal	_Dec_;
non terminal	_Exp2_;
non terminal	_ExternalDeclaration_;
non terminal	_Ident_;
non terminal	_InitDeclarator_;
non terminal	_Initializer_;
non terminal	_ParameterDeclaration_;
//non terminal	_PrintfParam_;
non terminal	_Stm_;
 
non terminal	AssignmentOp;
non terminal	CompoundStm;
non terminal	Constant;
non terminal	ConstantExpression;
non terminal	Dec;
non terminal	DirectDeclarator;
non terminal	Exp;
non terminal	Exp2;
non terminal	Exp3;
non terminal	Exp4;
non terminal	Exp5;
non terminal	Exp6;
non terminal	Exp7;
non terminal	Exp8;
non terminal	Exp9;
non terminal	Exp10;
non terminal	Exp11;
non terminal	Exp12;
non terminal	ExpressionStm;
non terminal	ExternalDeclaration;
non terminal	FunctionDef;
non terminal	GetsStm;
non terminal	InitDeclarator;
non terminal	Initializer;
non terminal	IterStm;
non terminal	JumpStm;
non terminal	ParameterDeclaration;
non terminal	PrintfStm;
non terminal	Program;
non terminal	SelectionStm;
non terminal	Stm;
non terminal	TypeSpecifier;

/* Precedences */
precedence left ELSE;

start with Program;

/* The grammar */
// Inicio del programa
 Program ::= _ExternalDeclaration_:led{:
	System.out.println("Program");
	NodeAnalyzer nodo =new NodeAnalyzer("Program");
	nodo.copyHijos(((NodeAnalyzer)led).getHijos());
	parser.setMain(nodo);
	RESULT = nodo; :};

// Declaraciones globales
 _ExternalDeclaration_ ::= ExternalDeclaration:ed {:
	System.out.println("_ExternalDeclaration_");
	NodeAnalyzer nodo =new NodeAnalyzer("_ExternalDeclaration_");
	nodo.copyLeftHijos(((NodeAnalyzer)ed).extractHermanos());
	nodo.addLeftHijo((NodeAnalyzer)ed);
	RESULT = nodo; :};
 _ExternalDeclaration_ ::= ExternalDeclaration:ed _ExternalDeclaration_:led {:
	System.out.println("_ExternalDeclaration_");
	((NodeAnalyzer)led).copyLeftHijos(((NodeAnalyzer)ed).extractHermanos());
	((NodeAnalyzer)led).addLeftHijo((NodeAnalyzer)ed);
	RESULT = led; :};
 _ExternalDeclaration_ ::= error _ExternalDeclaration_:led {:
	parser.addError("Error sintactico.", -1);
	System.out.println("error _ExternalDeclaration_");
	NodeAnalyzer nodo =new NodeAnalyzer("_ExternalDeclaration_");
	nodo.setError(true);
	((NodeAnalyzer)led).addLeftHijo(nodo);
	RESULT = led;
 :};

 ExternalDeclaration ::= FunctionDef:fd {:
	System.out.println("ExternalDeclaration");
	StateAnalyzer sa = parser.getState();
	if(sa.isDecStates(StateAnalyzer.DeclarationStates.GLOBAL))
		sa.changeDecStates();
	//((NodeAnalyzer)fd).setStates(sa.getDecStates().name());
	RESULT = fd;
 :};
 ExternalDeclaration ::= Dec:d {:
 	NodeAnalyzer nad = ((NodeAnalyzer)d);
	System.out.println("ExternalDeclaration");
	StateAnalyzer sa = parser.getState();
	if(!sa.isDecStates(StateAnalyzer.DeclarationStates.GLOBAL)){
		parser.addError("Declaracion de variable global entre funciones.", -1);
		nad.setError(true);
	}
	//nad.setStates(sa.getDecStates().name());
	RESULT = nad;
 :};
 
// Declaracion de funciones
 FunctionDef ::= TypeSpecifier:ts DirectDeclarator:dd CompoundStm:cs {:
	System.out.println("FunctionDef");
	NodeAnalyzer nodo =new NodeAnalyzer("FunctionDef");
	nodo.addLeftHijo((NodeAnalyzer)cs);
	//nodo.addLeftHijo((NodeAnalyzer)dd);
	nodo.copyLeftHijos(((NodeAnalyzer)dd).getHijos());
	String id = ((NodeAnalyzer)dd).getStrAtr(NodeKeys.VAR_ID);
	String type = ((NodeAnalyzer)ts).getStrAtr(NodeKeys.TYPE);
	if(!("void".compareTo(type)==0)){
		NodeAnalyzer ret = nodo.getLastChildren().getLastChildren();
		ret.setAtribute(NodeKeys.TYPE, type);
	}
	nodo.setAtribute(NodeKeys.TYPE, type);
	nodo.setAtribute(NodeKeys.FUNC_ID, id);
	nodo.setNodeType(NodeType.FUNCTION_DEC);
	VarTable.getInstance().addFunction(id, type);
	RESULT = nodo;
 :};

// Declaracion de variables
 Dec ::= TypeSpecifier:ts _InitDeclarator_:lid SEMICOLON {:
	System.out.println("Dec");
 	
 	NodeAnalyzer nats = (NodeAnalyzer)ts;
 	String type = nats.getStrAtr(NodeKeys.TYPE);
 	NodeAnalyzer nodo = null;
 	NodeAnalyzer aux = null;
 	LinkedList<NodeAnalyzer> hijos = ((NodeAnalyzer)lid).getHijos();
 	
 	for (NodeAnalyzer nah : hijos) {
		aux =new NodeAnalyzer("Dec");
		aux.setAtribute(NodeKeys.TYPE, type);
		aux.setAtribute(NodeKeys.VAR_ID, nah.getStrAtr(NodeKeys.VAR_ID));
		//TODO falta comprobar el tipo de la asignacion
		String cad = nah.getStrAtr(NodeKeys.CONST_INT_VALUE);
		if(cad != null)
			aux.setAtribute(NodeKeys.CONST_INT_VALUE, cad);
		
		cad = nah.getStrAtr(NodeKeys.CONST_CHAR_VALUE);
		if(cad != null)
			aux.setAtribute(NodeKeys.CONST_CHAR_VALUE, cad);
		
		cad = nah.getStrAtr(NodeKeys.CONST_FLOAT_VALUE);
		if(cad != null)
			aux.setAtribute(NodeKeys.CONST_FLOAT_VALUE, cad);
		
		aux.copyHijos(nah.getHijos());
		aux.setNodeType(NodeType.VARDEC);
		
		if(nodo == null){
			nodo = aux;
		}else{
			nodo.addRightHermano(aux);
		}
	}
 	
 	if(type.equals("void")){
 		parser.addError("Variable declarad con tipo Void.", -1);
 		nodo.setError(true);
 	}
	RESULT = nodo;
 :};

 _Dec_ ::= Dec:d {:
	System.out.println("_Dec_");
	NodeAnalyzer nodo =new NodeAnalyzer("_Dec_");
	nodo.addLeftHijo((NodeAnalyzer)d);
	RESULT = nodo;
 :};
 _Dec_ ::= Dec:d _Dec_:ld {:
	System.out.println("_Dec_");
	((NodeAnalyzer)ld).addLeftHijo((NodeAnalyzer)d);
	RESULT = ld;
 :};
 
/* _Dec_ ::= error _Dec_:ld {:
	parser.addError("Error sintactico.", -1);
	System.out.println("error _Dec_");
	NodeAnalyzer nodo =new NodeAnalyzer("_Dec_");
	nodo.setError(true);
	((NodeAnalyzer)ld).addLeftHijo(nodo);
	RESULT = ld;
 :}; */
 
 _InitDeclarator_ ::= InitDeclarator:id {:
	System.out.println("_InitDeclarator_");
	NodeAnalyzer nodo =new NodeAnalyzer("_InitDeclarator_");
	nodo.addLeftHijo((NodeAnalyzer)id);
	RESULT = nodo;
 :};
 _InitDeclarator_ ::= InitDeclarator:id COMMA _InitDeclarator_:lid {:
	System.out.println("_InitDeclarator_");
	((NodeAnalyzer)lid).addLeftHijo((NodeAnalyzer)id);
	RESULT = lid;
 :};

 InitDeclarator ::= DirectDeclarator:dd {:
	System.out.println("InitDeclarator");
	//NodeAnalyzer nodo =new NodeAnalyzer("InitDeclarator");
	RESULT = dd;
 :};
 InitDeclarator ::= DirectDeclarator:dd EQ Initializer:i {:
	System.out.println("InitDeclarator");
	
 	NodeAnalyzer nadd = (NodeAnalyzer)dd;
 	NodeAnalyzer nai = (NodeAnalyzer)i;
 	
 	String cad = nai.getStrAtr(NodeKeys.CONST_INT_VALUE);
	if(cad != null)
		nadd.setAtribute(NodeKeys.CONST_INT_VALUE, cad);
	else{
		cad = nai.getStrAtr(NodeKeys.CONST_CHAR_VALUE);
		if(cad != null)
			nadd.setAtribute(NodeKeys.CONST_CHAR_VALUE, cad);
		else{
			cad = nai.getStrAtr(NodeKeys.CONST_FLOAT_VALUE);
			if(cad != null)
				nadd.setAtribute(NodeKeys.CONST_FLOAT_VALUE, cad);
			else
				nadd.addLeftHijo(nai);
		}
	}
	RESULT = dd;
 :};

// Declaracion de tipos
 TypeSpecifier ::= VOID {:
	System.out.println("TypeSpecifier");
	NodeAnalyzer nodo =new NodeAnalyzer("TypeSpecifier");
	nodo.setAtribute(NodeKeys.TYPE, "void");
	RESULT = nodo;
 :};
 TypeSpecifier ::= CHAR:c {:
	System.out.println("TypeSpecifier");
	NodeAnalyzer nodo =new NodeAnalyzer("TypeSpecifier");
	nodo.setAtribute(NodeKeys.TYPE, "char");
	nodo.setAtribute(NodeKeys.CONST_CHAR_VALUE, ""+c.charValue());
	RESULT = nodo;
 :};
 TypeSpecifier ::= INT:i {:
	System.out.println("TypeSpecifier");
	NodeAnalyzer nodo =new NodeAnalyzer("TypeSpecifier");
	nodo.setAtribute(NodeKeys.TYPE, "int");
	nodo.setAtribute(NodeKeys.CONST_INT_VALUE, ""+i.intValue());
	RESULT = nodo;
 :};
 TypeSpecifier ::= FLOAT:f {:
	System.out.println("TypeSpecifier");
	NodeAnalyzer nodo =new NodeAnalyzer("TypeSpecifier");
	nodo.setAtribute(NodeKeys.TYPE, "float");
	nodo.setAtribute(NodeKeys.CONST_FLOAT_VALUE, ""+f.floatValue());
	RESULT = nodo;
 :};

 DirectDeclarator ::= ID:id {:
	System.out.println("DirectDeclarator");
	NodeAnalyzer nodo =new NodeAnalyzer("DirectDeclarator");
	//nodo.setStates(parser.getState().getDecStates().name());
	nodo.setNodeType(NodeType.VAR);
	nodo.setAtribute(NodeKeys.VAR_ID, id.toString());
	RESULT = nodo;
 :};
// Creo que no necesitamos el tipo de declaracion entre parentesis
/* DirectDeclarator ::= LPAREN DirectDeclarator RPAREN {:
	System.out.println("DirectDeclarator");
	NodeAnalyzer nodo =new NodeAnalyzer("DirectDeclarator");
	RESULT = nodo;
 :};*/
 DirectDeclarator ::= DirectDeclarator:dd LBRACK ConstantExpression:ce RBRACK {:
	System.out.println("DirectDeclarator");
 	NodeAnalyzer nadd = (NodeAnalyzer)dd;
 	switch (nadd.getNodeType()) {
	case VAR:
		nadd.setNodeType(NodeType.ARRAYVAR);
		nadd.setAtribute(NodeKeys.ARRAY_DIM, "1");
		nadd.addLeftHijo((NodeAnalyzer)ce);
		break;
	case ARRAYVAR:
		Integer i = nadd.getIntAtr(NodeKeys.ARRAY_DIM);
		nadd.setAtribute(NodeKeys.ARRAY_DIM, ""+(i.intValue()+1));
		nadd.addLeftHijo((NodeAnalyzer)ce);
		break;

	default:
		parser.addError("Incorrecta declaracion de array.", -1);
		nadd.setError(true);
		break;
	}
 	
	RESULT = nadd;
 :};
// TODO: Creo que no necesitamos esto (declaracion de arrays sin tamaño) usar como producción de error?
/* DirectDeclarator ::= DirectDeclarator:dd LBRACK RBRACK {:
	System.out.println("DirectDeclarator");
	NodeAnalyzer nodo =new NodeAnalyzer("DirectDeclarator");
	RESULT = nodo;
 :};*/
 DirectDeclarator ::= DirectDeclarator:dd LPAREN _ParameterDeclaration_:lpd RPAREN {:
	System.out.println("DirectDeclarator");
 	NodeAnalyzer nadd = (NodeAnalyzer)dd;
 	switch (nadd.getNodeType()) {
	case VAR:
		nadd.setNodeType(NodeType.FUNCTION_DEC);
		nadd.copyHijos(((NodeAnalyzer)lpd).getHijos());
		break;
	default:
		parser.addError("Incorrecta llamada de funcion.", -1);
		nadd.setError(true);
		break;
	}
 	parser.auxCountParamDec = 0;
 	RESULT = nadd;
 :};
 DirectDeclarator ::= DirectDeclarator:dd LPAREN _Ident_ RPAREN {:
	System.out.println("DirectDeclarator");
 	NodeAnalyzer nadd = (NodeAnalyzer)dd;
	switch (nadd.getNodeType()) {
	case VAR:
		nadd.setNodeType(NodeType.FUNCTION_CALL);
		break;
	default:
		parser.addError("Incorrecta llamada de funcion.", -1);
		nadd.setError(true);
		break;
	}
	
	RESULT = nadd;
 :};
 DirectDeclarator ::= DirectDeclarator:dd LPAREN RPAREN {:
	System.out.println("DirectDeclarator");
 	NodeAnalyzer nadd = (NodeAnalyzer)dd;
	switch (nadd.getNodeType()) {
	case VAR:
		nadd.setNodeType(NodeType.FUNCTION);
		break;
	default:
		parser.addError("Incorrecta declaracion de funcion.", -1);
		nadd.setError(true);
		break;
	}
	
	RESULT = nadd;
 :};

// Declaracion de parametros
 _ParameterDeclaration_ ::= ParameterDeclaration:pd {:
	System.out.println("_ParameterDeclaration_");
	NodeAnalyzer nodo =new NodeAnalyzer("_ParameterDeclaration_");
	nodo.addRightHijo((NodeAnalyzer)pd);
	((NodeAnalyzer)pd).setAtribute(NodeKeys.PARAM_POS, ""+parser.auxCountParamDec++);
	RESULT = nodo;
 :}; 
 _ParameterDeclaration_ ::= _ParameterDeclaration_:lpd COMMA ParameterDeclaration:pd {:
	System.out.println("_ParameterDeclaration_");
	((NodeAnalyzer)lpd).addRightHijo((NodeAnalyzer)pd);
	((NodeAnalyzer)pd).setAtribute(NodeKeys.PARAM_POS, ""+parser.auxCountParamDec++);
	RESULT = lpd;
 :};

 ParameterDeclaration ::= TypeSpecifier:ts DirectDeclarator:dd {:
	System.out.println("ParameterDeclaration");
	NodeAnalyzer nodo =new NodeAnalyzer("ParameterDeclaration");
	NodeAnalyzer nadd = (NodeAnalyzer)dd;
	nodo.setAtribute(NodeKeys.TYPE, ((NodeAnalyzer)ts).getStrAtr(NodeKeys.TYPE));
 	switch (nadd.getNodeType()) {
	case VAR:
		nodo.setAtribute(NodeKeys.VAR_ID, nadd.getStrAtr(NodeKeys.VAR_ID));
		nodo.setAtribute(NodeKeys.IS_PARAM, "true");
		nodo.setNodeType(NodeType.VARDEC);
		break;
	case ARRAYVAR:
		nodo.setAtribute(NodeKeys.VAR_ID, nadd.getStrAtr(NodeKeys.VAR_ID));
		nodo.setAtribute(NodeKeys.IS_PARAM, "true");
		nodo.setNodeType(NodeType.ARRAYVARDEC);
		break;

	default:
		parser.addError("Incorrecta declaracion de parametro.", -1);
		nodo.setError(true);
		break;
	}
 	
	RESULT = nodo;
 :};

// Declaracion de identificaores
 _Ident_ ::= ID {:
	System.out.println("_Ident_");
	NodeAnalyzer nodo =new NodeAnalyzer("_Ident_");
	RESULT = nodo;
 :};
 _Ident_ ::= ID COMMA _Ident_ {:
	System.out.println("_Ident_");
	NodeAnalyzer nodo =new NodeAnalyzer("_Ident_");
	RESULT = nodo;
 :};

// Inicializadores de identificadores
 Initializer ::= Exp2:e2 {:
	System.out.println("Initializer");
 	NodeAnalyzer nae2 = (NodeAnalyzer)e2; 
 	nae2.setNodeType(NodeType.ASIGNATION);
 	nae2.setAtribute(NodeKeys.ASIGNMENT, "=");
	RESULT = nae2;
 :};
 Initializer ::= LBRACE _Initializer_ RBRACE {:
	System.out.println("Initializer");
	NodeAnalyzer nodo =new NodeAnalyzer("Initializer");
	RESULT = nodo;
 :};
 Initializer ::= LBRACE _Initializer_ COMMA RBRACE {:
	System.out.println("Initializer");
	NodeAnalyzer nodo =new NodeAnalyzer("Initializer");
	RESULT = nodo;
 :};

 _Initializer_ ::= Initializer {:
	System.out.println("_Initializer_");
	NodeAnalyzer nodo =new NodeAnalyzer("_Initializer_");
	RESULT = nodo;
 :};
 _Initializer_ ::= _Initializer_ COMMA Initializer {:
	System.out.println("_Initializer_");
	NodeAnalyzer nodo =new NodeAnalyzer("_Initializer_");
	RESULT = nodo;
 :};

// Sentencias
 Stm ::= CompoundStm:cs {:
	System.out.println("Stm");
	RESULT = cs;
 :};
 Stm ::= ExpressionStm:es {:
	System.out.println("Stm");
	RESULT = es;
 :};
 Stm ::= SelectionStm:ss {:
	System.out.println("Stm");
	RESULT = ss;
 :};
 Stm ::= IterStm:is {:
	System.out.println("Stm");
	RESULT = is;
 :};
 Stm ::= JumpStm:js {:
	System.out.println("Stm");
	RESULT = js;
 :};

 CompoundStm ::= LBRACE RBRACE {:
	System.out.println("CompoundStm");
	NodeAnalyzer nodo =new NodeAnalyzer("CompoundStm");
	parser.addWarning("Cuerpo de sentencias vacio.");
	RESULT = nodo;
 :};
 CompoundStm ::= LBRACE _Stm_:ls RBRACE {:
	System.out.println("CompoundStm");
	NodeAnalyzer nodo =new NodeAnalyzer("CompoundStm");
	nodo.copyHijos(((NodeAnalyzer)ls).getHijos());
	RESULT = nodo;
 :};
 CompoundStm ::= LBRACE _Dec_:ld RBRACE {:
	System.out.println("CompoundStm");
	NodeAnalyzer nodo =new NodeAnalyzer("CompoundStm");
	nodo.copyHijos(((NodeAnalyzer)ld).getHijos());
	RESULT = nodo;
 :};
 CompoundStm ::= LBRACE _Dec_:ld _Stm_:ls RBRACE {:
	System.out.println("CompoundStm");
	NodeAnalyzer nodo =new NodeAnalyzer("CompoundStm");
	nodo.copyHijos(((NodeAnalyzer)ld).getHijos());
	nodo.copyHijos(((NodeAnalyzer)ls).getHijos());
	RESULT = nodo;
 :};

 ExpressionStm ::= SEMICOLON {:
	System.out.println("ExpressionStm");
	NodeAnalyzer nodo =new NodeAnalyzer("ExpressionStm");
	parser.addWarning("Expresion vacia.");
	RESULT = nodo;
 :};
 ExpressionStm ::= Exp:e SEMICOLON {:
	System.out.println("ExpressionStm");
	NodeAnalyzer nodo =new NodeAnalyzer("ExpressionStm");
	nodo.addLeftHijo((NodeAnalyzer)e);
	RESULT = nodo;
 :};
 ExpressionStm ::= PrintfStm:pf SEMICOLON {:
	System.out.println("ExpressionStm");
	RESULT = pf;
 :};
 ExpressionStm ::= GetsStm:gs SEMICOLON {:
	System.out.println("ExpressionStm");
	RESULT = gs;
 :};

 SelectionStm ::= IF LPAREN Exp:e RPAREN Stm:s {:
	System.out.println("SelectionStm");
	NodeAnalyzer nodo =new NodeAnalyzer("SelectionStm");
	nodo.setNodeType(NodeType.IF);
	nodo.addLeftHijo((NodeAnalyzer)e);
	nodo.addLeftHijo((NodeAnalyzer)s);
	RESULT = nodo;
 :};
 SelectionStm ::= IF LPAREN Exp:e RPAREN Stm:s ELSE Stm:se {:
	System.out.println("SelectionStm");
	NodeAnalyzer nodo =new NodeAnalyzer("SelectionStm");
	nodo.setNodeType(NodeType.IFELSE);
	nodo.addLeftHijo((NodeAnalyzer)e);
	nodo.addLeftHijo((NodeAnalyzer)s);
	nodo.addLeftHijo((NodeAnalyzer)se);
	RESULT = nodo;
 :};

 IterStm ::= WHILE LPAREN Exp:e RPAREN Stm:s {:
	System.out.println("IterStm");
	NodeAnalyzer nodo =new NodeAnalyzer("IterStm");
	nodo.addLeftHijo((NodeAnalyzer)e);
	nodo.addLeftHijo((NodeAnalyzer)s);
	RESULT = nodo;
 :};

 JumpStm ::= RETURN Exp:e SEMICOLON {:
	System.out.println("JumpStm");
	NodeAnalyzer nodo =new NodeAnalyzer("JumpStm");
	nodo.setNodeType(NodeType.RETURN);
	nodo.addLeftHijo((NodeAnalyzer)e);
	RESULT = nodo;
 :};
 
 PrintfStm ::= PRINTF LPAREN STRING COMMA STRING:s RPAREN {:
	System.out.println("PrintfStm");
	NodeAnalyzer nodo =new NodeAnalyzer("PrintfStm");
	nodo.setNodeType(NodeType.PRINTFSTRING);
	nodo.setAtribute(NodeKeys.STRING, s);
	RESULT = nodo;
 :};
 
 PrintfStm ::= PRINTF LPAREN STRING COMMA ID:id RPAREN {:
	System.out.println("PrintfStm");
	NodeAnalyzer nodo =new NodeAnalyzer("PrintfStm");
	nodo.setNodeType(NodeType.PRINTFID);
	nodo.setAtribute(NodeKeys.VAR_ID, (String)id);
	RESULT = nodo;
 :};
 
 /*PrintfStm ::= PRINTF LPAREN STRING:s _PrintfParam_:lpp RPAREN {:
	System.out.println("PrintfStm");
	NodeAnalyzer nodo =new NodeAnalyzer("PrintfStm");
	nodo.setAtribute(NodeKeys.STRING, s);
	nodo.copyHijos(((NodeAnalyzer)lpp).getHijos());
	RESULT = nodo;
 :};
 
 _PrintfParam_ ::= {:
	System.out.println("_PrintfParam_");
	NodeAnalyzer nodo =new NodeAnalyzer("_PrintfParam_");
	RESULT = nodo;
 :};
 _PrintfParam_ ::= COMMA ID:id _PrintfParam_:lpp {:
	System.out.println("_PrintfParam_");
	NodeAnalyzer nodo =new NodeAnalyzer("PrintfParam");
	nodo.setAtribute(NodeKeys.VAR_ID, id.toString());
	((NodeAnalyzer)lpp).addRightHijo(nodo);
	RESULT = lpp;
 :};*/
 
 GetsStm ::= GETS LPAREN ID RPAREN {:
	System.out.println("GetsStm");
	NodeAnalyzer nodo =new NodeAnalyzer("GetsStm");
	RESULT = nodo;
 :};

 _Stm_ ::= Stm:s {:
	System.out.println("_Stm_");
	NodeAnalyzer nodo =new NodeAnalyzer("_Stm_");
	nodo.addLeftHijo((NodeAnalyzer)s);
	RESULT = nodo;
 :};
 _Stm_ ::= Stm:s _Stm_:ls {:
	System.out.println("_Stm_");
 	((NodeAnalyzer)ls).addLeftHijo((NodeAnalyzer)s);
	RESULT = ls;
 :};
_Stm_ ::= error _Stm_:ls {:
	parser.addError("Error sintactico.", -1);
	System.out.println("error _Stm_");
	NodeAnalyzer nodo =new NodeAnalyzer("_Stm_");
	nodo.setError(true);
	((NodeAnalyzer)ls).addLeftHijo(nodo);
	RESULT = ls;
 :};

// Expresiones
 Exp ::= Exp2:e2 {:
	System.out.println("Exp");
	RESULT = e2;
 :};
 Exp ::= Exp COMMA Exp2 {:
	System.out.println("Exp");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp");
	RESULT = nodo;
 :};
 
 Exp2 ::= Exp3:e3 {:
	System.out.println("Exp2");
	RESULT = e3;
 :};
 Exp2 ::= Exp9:e9 AssignmentOp:ao Exp2:e2 {:
	System.out.println("Exp2");
 	NodeAnalyzer nodo =new NodeAnalyzer("Exp2");
	nodo.addLeftHijo((NodeAnalyzer)e2);
	NodeAnalyzer reciver = (NodeAnalyzer)e9;
	nodo.setNodeType(NodeType.ASIGNATION);
	reciver.setCad("reciver("+reciver.getCad()+")");
	nodo.addLeftHijo(reciver);
	nodo.setAtribute(NodeKeys.ASIGNMENT, ((NodeAnalyzer)ao).getStrAtr(NodeKeys.ASIGNMENT));
	RESULT = nodo;
 :};
 
 Exp3 ::= Exp4:e4 {:
	System.out.println("Exp3");
	RESULT = e4;
 :};
 Exp3 ::= Exp3:e3 OROR Exp4:e4 {:
	System.out.println("Exp3");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp3");
	nodo.setNodeType(NodeType.OPERATION);
	nodo.addLeftHijo((NodeAnalyzer)e4);
	nodo.addLeftHijo((NodeAnalyzer)e3);
	nodo.setAtribute(NodeKeys.OPERATION, "||");
	RESULT = nodo;
 :};
 
 Exp4 ::= Exp5:e5 {:
	System.out.println("Exp4");
	RESULT = e5;
 :};
 Exp4 ::= Exp4:e4 ANDAND Exp5:e5 {:
	System.out.println("Exp4");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp4");
	nodo.setNodeType(NodeType.OPERATION);
	nodo.addLeftHijo((NodeAnalyzer)e5);
	nodo.addLeftHijo((NodeAnalyzer)e4);
	nodo.setAtribute(NodeKeys.OPERATION, "&&");
	RESULT = nodo;
 :};

 Exp5 ::= Exp6:e6 {:
	System.out.println("Exp5");
	RESULT = e6;
 :};
 Exp5 ::= Exp5:e5 EQEQ Exp6:e6 {:
	System.out.println("Exp5");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp5");
	nodo.setNodeType(NodeType.OPERATION);
	nodo.addLeftHijo((NodeAnalyzer)e6);
	nodo.addLeftHijo((NodeAnalyzer)e5);
	nodo.setAtribute(NodeKeys.OPERATION, "==");
	RESULT = nodo;
 :};
 Exp5 ::= Exp5:e5 NOTEQ Exp6:e6 {:
	System.out.println("Exp5");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp5");
	nodo.setNodeType(NodeType.OPERATION);
	nodo.addLeftHijo((NodeAnalyzer)e6);
	nodo.addLeftHijo((NodeAnalyzer)e5);
	nodo.setAtribute(NodeKeys.OPERATION, "!=");
	RESULT = nodo;
 :};
 
 Exp6 ::= Exp7:e7 {:
	System.out.println("Exp6");
	RESULT = e7;
 :};
 Exp6 ::= Exp6:e6 LT Exp7:e7 {:
	System.out.println("Exp6");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp6");
	nodo.setNodeType(NodeType.OPERATION);
	nodo.addLeftHijo((NodeAnalyzer)e7);
	nodo.addLeftHijo((NodeAnalyzer)e6);
	nodo.setAtribute(NodeKeys.OPERATION, "<");
	RESULT = nodo;
 :};
 Exp6 ::= Exp6:e6 GT Exp7:e7 {:
	System.out.println("Exp6");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp6");
	nodo.setNodeType(NodeType.OPERATION);
	nodo.addLeftHijo((NodeAnalyzer)e7);
	nodo.addLeftHijo((NodeAnalyzer)e6);
	nodo.setAtribute(NodeKeys.OPERATION, ">");
	RESULT = nodo;
 :};
 Exp6 ::= Exp6:e6 LTEQ Exp7:e7 {:
	System.out.println("Exp6");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp6");
	nodo.setNodeType(NodeType.OPERATION);
	nodo.addLeftHijo((NodeAnalyzer)e7);
	nodo.addLeftHijo((NodeAnalyzer)e6);
	nodo.setAtribute(NodeKeys.OPERATION, "<=");
	RESULT = nodo;
 :};
 Exp6 ::= Exp6:e6 GTEQ Exp7:e7 {:
	System.out.println("Exp6");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp6");
	nodo.setNodeType(NodeType.OPERATION);
	nodo.addLeftHijo((NodeAnalyzer)e7);
	nodo.addLeftHijo((NodeAnalyzer)e6);
	nodo.setAtribute(NodeKeys.OPERATION, ">=");
	RESULT = nodo;
 :};
 
 Exp7 ::= Exp8:e8 {:
	System.out.println("Exp7");
	RESULT = e8;
 :};
 Exp7 ::= Exp7:e7 PLUS Exp8:e8 {:
	System.out.println("Exp7");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp7");
	nodo.setNodeType(NodeType.OPERATION);
	nodo.addLeftHijo((NodeAnalyzer)e8);
	nodo.addLeftHijo((NodeAnalyzer)e7);
	nodo.setAtribute(NodeKeys.OPERATION, "+");
	RESULT = nodo;
 :};
 Exp7 ::= Exp7:e7 MINUS Exp8:e8 {:
	System.out.println("Exp7");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp7");
	nodo.setNodeType(NodeType.OPERATION);
	nodo.addLeftHijo((NodeAnalyzer)e8);
	nodo.addLeftHijo((NodeAnalyzer)e7);
	nodo.setAtribute(NodeKeys.OPERATION, "-");
	RESULT = nodo;
 :};
 
 Exp8 ::= Exp12:e12 {:
	System.out.println("Exp8");
	RESULT = e12;
 :};
 Exp8 ::= Exp8:e8 MULT Exp12:e12 {:
	System.out.println("Exp8");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp8");
	nodo.setNodeType(NodeType.OPERATION);
	nodo.addLeftHijo((NodeAnalyzer)e12);
	nodo.addLeftHijo((NodeAnalyzer)e8);
	nodo.setAtribute(NodeKeys.OPERATION, "*");
	RESULT = nodo;
 :};
 Exp8 ::= Exp8:e8 DIV Exp12:e12 {:
	System.out.println("Exp8");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp8");
	nodo.setNodeType(NodeType.OPERATION);
	nodo.addLeftHijo((NodeAnalyzer)e12);
	nodo.addLeftHijo((NodeAnalyzer)e8);
	nodo.setAtribute(NodeKeys.OPERATION, "/");
	RESULT = nodo;
 :};
 
 Exp12 ::= Exp9:e9 {:
	System.out.println("Exp12");
	RESULT = e9;
 :};
 Exp12 ::= LPAREN TypeSpecifier RPAREN Exp12 {:
	System.out.println("Exp12");
 	//TODO conversor
	NodeAnalyzer nodo =new NodeAnalyzer("Exp12");
	RESULT = nodo;
 :};
 
 Exp9 ::= Exp10:e10 {:
	System.out.println("Exp9");
	RESULT = e10;
 :};
 Exp9 ::= PLUSPLUS Exp9 {:
	System.out.println("Exp9");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp9");
	RESULT = nodo;
 :};
 Exp9 ::= MINUSMINUS Exp9 {:
	System.out.println("Exp9");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp9");
	RESULT = nodo;
 :};
 Exp9 ::= NOT Exp12 {:
	System.out.println("Exp9");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp9");
	RESULT = nodo;
 :};
 
 Exp10 ::= Exp11:e11 {:
	System.out.println("Exp10");
	RESULT = e11;
 :};
 Exp10 ::= Exp10 LBRACK Exp RBRACK {:
	System.out.println("Exp10");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp10");
	RESULT = nodo;
 :};
 Exp10 ::= Exp10:e10 LPAREN RPAREN {:
	System.out.println("Exp10");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp10");
	nodo.setNodeType(NodeType.FUNCTION_CALL);
	String id = ((NodeAnalyzer)e10).getStrAtr(NodeKeys.VAR_ID);
	if(id != null)
		nodo.setAtribute(NodeKeys.FUNC_ID, id.toString());
	else{
		//TODO error
		System.out.println("error");
	}
	RESULT = nodo;
 :};
 Exp10 ::= Exp10:e10 LPAREN _Exp2_:e2 RPAREN {:
	System.out.println("Exp10");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp10");
	nodo.setNodeType(NodeType.FUNCTION_CALL);
	String id = ((NodeAnalyzer)e10).getStrAtr(NodeKeys.VAR_ID);
	if(id != null)
		nodo.setAtribute(NodeKeys.FUNC_ID, id.toString());
	else{
		//TODO error
		System.out.println("error");
	}
	nodo.copyHijos(((NodeAnalyzer)e2).getHijos());
	RESULT = nodo;
 :};
 Exp10 ::= Exp10 PLUSPLUS {:
	System.out.println("Exp10");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp10");
	RESULT = nodo;
 :};
 Exp10 ::= Exp10 MINUSMINUS {:
	System.out.println("Exp10");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp10");
	RESULT = nodo;
 :};
 
 Exp11 ::= LPAREN Exp:e RPAREN {:
	System.out.println("Exp11");
	//NodeAnalyzer nodo =new NodeAnalyzer("Exp11");
	//RESULT = nodo;
 	RESULT = e;
 :};
 Exp11 ::= ID:id {:
	System.out.println("Exp11");
	NodeAnalyzer nodo =new NodeAnalyzer("Exp11");
	//nodo.setStates(parser.getState().getDecStates().name());
	nodo.setNodeType(NodeType.VAR);
	nodo.setAtribute(NodeKeys.VAR_ID, id.toString());
	RESULT = nodo;
 :};
 Exp11 ::= Constant:c {:
	System.out.println("Exp11");
	RESULT = c;
 :};

 Constant ::= NUMBERF:f {:
	System.out.println("Constant");
	NodeAnalyzer nodo =new NodeAnalyzer("Constant");
	nodo.setNodeType(NodeType.CONSTANT);
	nodo.setAtribute(NodeKeys.TYPE, "float");
	nodo.setAtribute(NodeKeys.CONST_FLOAT_VALUE, ""+f.floatValue());
	RESULT = nodo;
 :};
 Constant ::= CHARACTER:c {:
	System.out.println("Constant");
	NodeAnalyzer nodo =new NodeAnalyzer("Constant");
	nodo.setNodeType(NodeType.CONSTANT);
	nodo.setAtribute(NodeKeys.TYPE, "char");
	nodo.setAtribute(NodeKeys.CONST_CHAR_VALUE, ""+c.charValue());
	RESULT = nodo;
 :};
 Constant ::= INTEGER:i {:
	System.out.println("Constant");
	NodeAnalyzer nodo =new NodeAnalyzer("Constant");
	nodo.setNodeType(NodeType.CONSTANT);
	nodo.setAtribute(NodeKeys.TYPE, "int");
	nodo.setAtribute(NodeKeys.CONST_INT_VALUE, ""+i.intValue());
	RESULT = nodo;
 :};

 ConstantExpression ::= Exp3:e3 {:
	System.out.println("ConstantExpression");
	NodeAnalyzer nodo =new NodeAnalyzer("ConstantExpression");
	RESULT = nodo ;
 :};

 _Exp2_ ::= Exp2:e2 {:
	System.out.println("_Exp2_");
	NodeAnalyzer nodo =new NodeAnalyzer("_Exp2_");
	nodo.addLeftHijo((NodeAnalyzer)e2);
	RESULT = nodo;
 :};
 _Exp2_ ::= Exp2:e2 COMMA _Exp2_:le2 {:
	System.out.println("_Exp2_");
 	((NodeAnalyzer)le2).addLeftHijo((NodeAnalyzer)e2);
	RESULT = le2;
 :};

// Simbolos de asignacion
 AssignmentOp ::= EQ {:
	System.out.println("AssignmentOp");
	NodeAnalyzer nodo =new NodeAnalyzer("AssignmentOp");
	nodo.setAtribute(NodeKeys.ASIGNMENT, "=");
	RESULT = nodo;
 :};
 AssignmentOp ::= MULTEQ {:
	System.out.println("AssignmentOp");
	NodeAnalyzer nodo =new NodeAnalyzer("AssignmentOp");
	nodo.setAtribute(NodeKeys.ASIGNMENT, "*=");
	RESULT = nodo;
 :};
 AssignmentOp ::= DIVEQ {:
	System.out.println("AssignmentOp");
	NodeAnalyzer nodo =new NodeAnalyzer("AssignmentOp");
	nodo.setAtribute(NodeKeys.ASIGNMENT, "/=");
	RESULT = nodo;
 :};
 AssignmentOp ::= PLUSEQ {:
	System.out.println("AssignmentOp");
	NodeAnalyzer nodo =new NodeAnalyzer("AssignmentOp");
	nodo.setAtribute(NodeKeys.ASIGNMENT, "+=");
	RESULT = nodo;
 :};
 AssignmentOp ::= MINUSEQ {:
	System.out.println("AssignmentOp");
	NodeAnalyzer nodo = new NodeAnalyzer("AssignmentOp");
	nodo.setAtribute(NodeKeys.ASIGNMENT, "-=");
	RESULT = nodo;
 :};
